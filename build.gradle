plugins {
	id 'maven-publish'
	id 'io.github.juuxel.loom-quiltflower' version '1.8.+'
	id 'org.quiltmc.loom' version '1.+'
}

archivesBaseName = project.archives_base_name
version = "${project.version}+${project.minecraft_version}"
group = project.maven_group

loom {
	accessWidenerPath = file("src/main/resources/malum.accesswidener")
	runs {
		datagen {
			server()

			name "Minecraft Data"
			vmArg "-Dfabric-api.datagen"
			vmArg "-Dfabric-api.datagen.output-dir=${file("src/main/generated")}"

			runDir "build/datagen"
		}
	}
}

sourceSets.main.resources.srcDirs += ['src/main/generated']

repositories {
	maven {
		url = 'https://api.modrinth.com/maven'
		content {
			includeGroup 'maven.modrinth'
		}
	}
	maven {
		name = 'Quilt'
		url = 'https://maven.quiltmc.org/repository/release'
	}
	maven {
		name = "TerraformersMC"
		url = "https://maven.terraformersmc.com/"
	}
	maven {
		name = 'Ladysnake Mods'
		url = 'https://ladysnake.jfrog.io/artifactory/mods'
	}
	maven {
		name = "JitPack"
		url = 'https://jitpack.io'
	}
	maven {
		name = "Reach Entity Attributes"
		url = "https://maven.jamieswhiteshirt.com/libs-release"
	}
	mavenCentral()
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"

	mappings "org.quiltmc:quilt-mappings:${project.minecraft_version}+build.${project.quilt_mappings}:intermediary-v2"
	modImplementation "org.quiltmc:quilt-loader:${project.quilt_loader}"
	modImplementation "org.quiltmc.quilted-fabric-api:quilted-fabric-api:${project.quilted_fabric_api_version}-${project.minecraft_version}"

	modImplementation("dev.emi:trinkets:${project.trinkets}") {
		exclude group: "net.fabricmc"
		exclude group: "net.fabricmc.fabric-api"
		exclude group: "com.terraformersmc"
	}

	modImplementation("dev.onyxstudios.cardinal-components-api:cardinal-components-base:${project.cca}") {
		exclude group: "net.fabricmc"
		exclude group: "net.fabricmc.fabric-api"
	}
	modImplementation("dev.onyxstudios.cardinal-components-api:cardinal-components-entity:${project.cca}") {
		exclude group: "net.fabricmc"
		exclude group: "net.fabricmc.fabric-api"
	}
	modImplementation("dev.onyxstudios.cardinal-components-api:cardinal-components-world:${project.cca}") {
		exclude group: "net.fabricmc"
		exclude group: "net.fabricmc.fabric-api"
	}

	modImplementation("com.github.Chocohead:Fabric-ASM:${project.fabric_asm}")
	include("com.github.Chocohead:Fabric-ASM:${project.fabric_asm}")

	include("io.github.ladysnake:satin:${project.satin}")
	modRuntimeOnly("maven.modrinth:sodium:${project.sodium}")

	modImplementation include("maven.modrinth:midnightlib:$midnightlib_version")
	modImplementation include("com.github.mrsterner:LodestoneLib-Quilt:$lodestone_version")
	modImplementation("com.jamieswhiteshirt:reach-entity-attributes:${project.reach}")

	implementation("com.github.LlamaLad7:MixinExtras:${project.mixin_extras}")
	include("com.github.LlamaLad7:MixinExtras:${project.mixin_extras}")
}

processResources {
	inputs.property 'version', version

	filesMatching('quilt.mod.json') {
		expand "version": version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = 'UTF-8'
	it.options.release = 17
}

java {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
	withSourcesJar()
}

jar {
	from('LICENSE') {
		rename { "${it}_${archivesBaseName}" }
	}
}

assemble.dependsOn runDatagen

